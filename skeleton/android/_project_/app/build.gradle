apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.1'

    defaultConfig {
        applicationId "com.kcompany.kproject"
        minSdkVersion 17
        targetSdkVersion 24
        versionCode 16091301
        versionName "1.0.0"
        multiDexEnabled true
    }

    signingConfigs {
        debug {
            storeFile file('android.keystore')
            storePassword 'androiddev'
            keyAlias 'android.keystore'
            keyPassword 'androiddev'
        }

        release {
            storeFile file('android.keystore')
            storePassword 'androiddev'
            keyAlias 'android.keystore'
            keyPassword 'androiddev'
        }
    }

    packagingOptions {
        // Exclude file to avoid
        // Error: Duplicate files during packaging of APK
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'README.txt'
    }

    lintOptions {
        abortOnError false
        //disable 'InvalidPackage'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            apply plugin: 'signing'
            apply plugin: 'checkstyle'
            minifyEnabled false
            zipAlignEnabled false // this is default for debug
            shrinkResources false
        }

        release {
            apply plugin: 'signing'
            apply plugin: 'checkstyle'
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def fileName = "v${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}_${variant.buildType.name}.apk"
                if (variant.buildType.name == 'debug'){
                    fileName = "v${defaultConfig.versionName}_${variant.productFlavors[0].name}_${variant.buildType.name}.apk"
                }
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }

    //sourceSets for different channels
    sourceSets {
        main {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            java.srcDirs = ['src/main/java']
            resources.srcDirs = ['src/main/java']
            aidl.srcDirs = ['src/main']
            renderscript.srcDirs = ['src/main']
            res.srcDirs = ['src/main/res']
            assets.srcDirs = ['src/main/assets']
        }

        storeShopDev {
            manifest.srcFile 'src/flavor/shop/dev/AndroidManifest.xml'
            assets.srcDirs = ['src/flavor/shop/dev/assets']
        }

        storeShopProd {
            manifest.srcFile 'src/flavor/shop/prod/AndroidManifest.xml'
            assets.srcDirs = ['src/flavor/shop/prod/assets']
        }

    }

    productFlavors {

        storeShopDev {
            applicationId 'com.inno.bwm.shop'
            signingConfig signingConfigs.debug
            versionName defaultConfig.versionName
            manifestPlaceholders = [channel: "shopdev"]
        }

        storeShopProd {
            signingConfig signingConfigs.release
            versionName defaultConfig.versionName
            applicationId 'com.inno.bwm.shop'
            manifestPlaceholders = [channel: "shopprod"]
        }

    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    //compile 'com.android.support:support-v4:23.0.0'
    compile 'com.android.support:appcompat-v7:24.1.1'
    compile 'com.android.support:design:24.1.1'
    compile 'com.android.support:recyclerview-v7:24.1.1'
    //compile 'com.facebook.fresco:fresco:0.6.1+'
    // https://github.com/chrisbanes/PhotoView
    androidTestCompile 'junit:junit:4.11'
    //androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    //androidTestCompile 'org.mockito:mockito-core:1.9.5'
    compile 'com.github.yamingd.android:bootstrap:2.2.0@aar'
    compile 'com.github.yamingd.android:sqlite-annotation:1.1.0'
    compile 'com.github.yamingd.android:sqlite-mapper:1.2.2@aar'
    // Logging
    compile 'com.jakewharton.timber:timber:3.1.0'
    compile 'com.google.protobuf:protobuf-java:2.6.0'
    // dagger
    compile 'com.google.dagger:dagger:2.0.1'
    apt 'com.google.dagger:dagger-compiler:2.0.1'
    provided 'org.glassfish:javax.annotation:10.0-b28'
    compile 'com.jakewharton:butterknife:6.1.0'
    // Event Bus
    compile 'com.squareup:otto:1.3.5'
    // Image Loading
    compile 'com.squareup.picasso:picasso:2.5.2'
    //compile 'com.facebook.fresco:fresco:0.6.0+'
    // HTTP
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.4.0'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile 'com.squareup.okio:okio:1.4.0'
    // Umeng
    compile 'com.umeng.analytics:analytics:latest.integration'
    compile 'com.path:android-priority-jobqueue:1.1.2'
    // cache
    compile 'com.jakewharton:disklrucache:2.0.2'
    // socket
    compile 'com.koushikdutta.async:androidasync:2.+'
    // https://github.com/hdodenhof/CircleImageView
    compile 'de.hdodenhof:circleimageview:1.3.0'
    compile 'com.nineoldandroids:library:2.4.0'

    compile 'com.bigkoo:alertview:1.0.1'
    compile 'com.commit451:PhotoView:1.2.4'
    compile 'com.marshalchen.ultimaterecyclerview:library:0.7.0'

    //
    //compile 'com.facebook.stetho:stetho:1.2.0'
    //compile project(':PushSDK')
}